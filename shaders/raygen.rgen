#version 460

#extension GL_EXT_ray_tracing : require

layout(location = 0) rayPayloadEXT vec3 payload;

layout(set = 1, binding = 0) uniform accelerationStructureEXT tlas;
layout(set = 1, binding = 1, rgba32f) uniform image2D image;

layout(set = 1, binding = 2) uniform CameraData
{
    mat4 inverseView;
    mat4 inverseProj;
    vec4 position;
    vec4 params;
} cam;

layout(push_constant) uniform PushConts
{
    uvec2 offset;
    uvec2 resolution;
} pc;

void main()
{
    const uvec2 globalID = gl_LaunchIDEXT.xy + pc.offset;

    if (globalID.x >= pc.resolution.x || globalID.y >= pc.resolution.y) {
        return;
    }

    const vec2 pixelCenter = vec2(globalID) + 0.5;
    const vec2 screenPos = pixelCenter / vec2(pc.resolution) * 2.0 - 1.0;

    vec4 target = cam.inverseProj * vec4(screenPos.x, screenPos.y, 1.0, 1.0);
    vec3 rayDir = normalize((cam.inverseView * vec4(normalize(target.xyz), 0.0)).xyz);
    vec3 rayOrigin = cam.position.xyz;

    payload = vec3(0.0);

    traceRayEXT(
        tlas,
        0,
        0xFF,
        0,
        0,
        0,
        rayOrigin,
        cam.params[2],
        rayDir,
        cam.params[3],
        0
    );

    imageStore(image, ivec2(globalID), vec4(payload, 1.0));
}
