#version 460

#extension GL_EXT_ray_tracing : require

layout(location = 0) rayPayloadEXT vec3 payload;

layout(set = 1, binding = 0) uniform accelerationStructureEXT tlas;
layout(set = 1, binding = 1, rgba32f) uniform image2D image;

layout(set = 1, binding = 2) uniform CameraData {
    mat4 inverseView;
    mat4 inverseProj;
    vec4 position;
    vec4 params;
} cam;

void main()
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.x, gl_LaunchIDEXT.y) + 0.5;
    const vec2 screenPos = pixelCenter / vec2(gl_LaunchSizeEXT.x, gl_LaunchSizeEXT.y) * 2.0 - 1.0;

    vec4 target = cam.inverseProj * vec4(screenPos.x, screenPos.y, 1.0, 1.0);
    vec3 rayDir = normalize((cam.inverseView * vec4(normalize(target.xyz), 0.0)).xyz);
    vec3 rayOrigin = cam.position.xyz;

    payload = vec3(0.0);

    traceRayEXT(
        tlas,
        0,
        0xFF,
        0,
        0,
        0,
        rayOrigin,
        cam.params[2],
        rayDir,
        cam.params[3],
        0
    );

    vec3 color = payload;

    color = color / (color + vec3(1.0));
    color = pow(color, vec3(1.0/2.2));

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(color, 1.0));
}
